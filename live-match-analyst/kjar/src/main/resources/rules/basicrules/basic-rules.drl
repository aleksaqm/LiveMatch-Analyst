package basicrules;

import com.ftn.sbnz.model.events.GameEvent;
import com.ftn.sbnz.model.events.GameEvent.EventType;
import com.ftn.sbnz.model.models.PlayerStats;
import com.ftn.sbnz.model.models.Player;
import com.ftn.sbnz.model.models.Team;
import com.ftn.sbnz.model.models.CommentaryLine;
import com.ftn.sbnz.model.models.CommentaryLine.Importance;
import com.ftn.sbnz.model.models.CommentaryLine.Type;

rule "ThreePointerComment"
when
    $event: GameEvent(eventType == EventType.SHOT_MADE, details["points"] == 3, $playerId: playerId, $teamId: teamId)
    $player: Player(id == $playerId, $playerName: name)
    $team: Team(id == $teamId, $teamName: name)
    $stats: PlayerStats(playerId == $playerId)
then
    modify($stats){
        setThreePointersMade($stats.getThreePointersMade() + 1),
        setPoints($stats.getPoints() + 3)
    }
    modify($event){
        setProcessed($stats.getThreePointersMade() < 6)
    }

    insert(new CommentaryLine("Trojka! Igrač " + $playerName + " iz tima " + $teamName + " je pogodio trojku!", Importance.LOW, Type.PLAY_BY_PLAY));
end

rule "SixthThreePointerComment"
when
    $event: GameEvent(eventType == EventType.SHOT_MADE, details["points"] == 3, processed == false, $playerId: playerId, $teamId: teamId)
    $stats: PlayerStats(playerId == $playerId, threePointersMade >= 6)
    $player: Player(id == $playerId, $playerName: name)
    $team: Team(id == $teamId, $teamName: name)
then
    modify($event){
        setProcessed(true)
    }
    insert(new CommentaryLine("Neverovatni " + $playerName + " iz tima " + $teamName + " nastavlja svoju seriju sa distance, ovo mu je već "  + $stats.getThreePointersMade() +  ". trojka!", Importance.HIGH, Type.HIGHLIGHT));
end

rule "FastbreakOpportunityComment"
when
    $event: GameEvent(eventType == EventType.TURNOVER, $teamId: teamId)
    $team: Team(id == $teamId)
    $opponent: Team(id != $teamId, $opponentName: name)
then
    insert(new CommentaryLine("Prilika za brzu kontru za tim " + $opponentName + "!", Importance.MEDIUM, Type.ANALYSIS));
end

rule "DefensiveStopStreakComment"
salience 100  
when
    $team: Team($teamId: id, $stoppedTeamName: name)
    $stops: Number(intValue >= 6) from accumulate (
        GameEvent(
            (eventType == EventType.SHOT_MISSED || eventType == EventType.TURNOVER),
            teamId == $teamId
        ) over window:time(2m),
        count(1)
    )
    not (GameEvent(eventType == EventType.SHOT_MADE, teamId == $teamId) over window:time(1m))
    $defendingTeam: Team(id != $teamId, $defendingTeamName: name)
then
    insert(new CommentaryLine("Odlična defanziva! Tim " + $defendingTeamName + " je prisiljavao " + $stoppedTeamName + " na " + $stops + " neuspešnih napada u poslednja 2 minuta.", Importance.HIGH, Type.ANALYSIS));
end


rule "ScoringRunComment"
when
    $team: Team($teamId: id, $teamName: name)
    $run: Number(intValue >= 8) from accumulate (
        GameEvent(
            eventType == EventType.SHOT_MADE,
            teamId == $teamId,
            $points: details['points']
        ) over window:time(2m),
        sum($points)
    )
    not (GameEvent(
        eventType == EventType.SHOT_MADE,
        teamId != $teamId
    ) over window:time(2m))
    $opposingTeam: Team(id != $teamId, $opposingTeamName: name)
then
    insert(new CommentaryLine(
        "Kakva serija tima " + $teamName + "! Rezultat je " + $run.intValue() + "-0 u poslednja dva minuta!",
        Importance.HIGH,
        Type.ANALYSIS
    ));
end

rule "AssistComment"
when
    $event: GameEvent(eventType == EventType.ASSIST, $playerId: playerId, $teamId: teamId)
    $player: Player(id == $playerId, $playerName: name)
    $team: Team(id == $teamId, $teamName: name)
    $stats: PlayerStats(playerId == $playerId)
then
    modify($stats){
        setAssists($stats.getAssists() + 1)
    }
    modify($event){
        setProcessed(true)
    }
    
    insert(new CommentaryLine("Asistencija od " + $playerName + " iz tima " + $teamName + "! To mu je " + ($stats.getAssists() + 1) + ". asistencija u meču.", Importance.LOW, Type.PLAY_BY_PLAY));
end

