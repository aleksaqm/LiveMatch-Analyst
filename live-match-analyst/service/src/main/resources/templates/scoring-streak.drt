template header
playerId
shotCount
timeWindowMinutes
commentText
importance

package com.ftn.sbnz.kjar.templates;
import com.ftn.sbnz.model.events.GameEvent;
import com.ftn.sbnz.model.models.CommentaryLine;
import com.ftn.sbnz.model.models.Player;
import com.ftn.sbnz.model.models.PlayerStats;

template "scoring_streak_template"

rule "Scoring_Streak_@{row.rowNumber}"
    when
        $player: Player(id == @{playerId}L)
        $stats: PlayerStats(playerId == @{playerId}L)
        accumulate (
            GameEvent(
                eventType == GameEvent.EventType.SHOT_MADE,
                playerId == @{playerId}L
            ) over window:time(@{timeWindowMinutes}m),
            $streakCount: count(1)
        )
        eval($streakCount >= @{shotCount})
        not (
            GameEvent(
                eventType == GameEvent.EventType.SHOT_MISSED,
                playerId == @{playerId}L
            ) over window:time(@{timeWindowMinutes}m)
        )
    then
        String dynamicComment = "@{commentText}" + " " + $player.getName() + " dao " + $streakCount + " Å¡uteva za redom  i sada ima ukupno " + $stats.getPoints() + " poena !";
        insert(new CommentaryLine(
            dynamicComment,
            CommentaryLine.Importance.valueOf("@{importance}"),
            CommentaryLine.Type.HIGHLIGHT
        ));
        System.out.println("Template rule triggered: " + $player.getName() + " (ID: @{playerId}) made " + $streakCount + " consecutive shots!");
end

end template